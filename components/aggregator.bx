class {

	public function init() {
		variables.rss = createObject('java', 'com.apptasticsoftware.rssreader.RssReader');
	}

	public function getBlog(required numeric id) {
		blog = queryExecute('
select id, name, url, rssurl
from blogs 
where id = :id
		', { id: id });

		if(blog.recordCount == 0) throw(message="Blog #url.id# does not exist.");
		return blog.rowData(1);

	}


	public function getBlogs() {
		return queryExecute('
select b.id, b.name, b.url, b.rssurl, count(i.blogidfk) as itemcount
from blogs b 
left join items i on b.id = i.blogidfk
group by b.id, b.name, b.url, b.rssurl
order by b.name asc
		');

	}

	/*
	Given a blog record, I fetch items for it via the RSS url.
	*/
	public function fetchItemsForFeed(f) { 
		items = variables.rss.read(f.rssurl).toList();
		return items.map(i => {
			result = {
				title:'',
				content:'',
				pubdate:'',
				link:''
			}

			i.getTitle().ifPresent(t => result.title = t);
			i.getLink().ifPresent(l => result.link = l);
			i.getContent().ifPresent(c => result.content = c);
			i.getDescription().ifPresent(c => {
				/*
				 So, some feeds had content, some had this. I think it is safe
				 to say that if this exists and content is blank, just overwrite.
				*/
				if(result.content == '') result.content = c;
			})
			i.getPubDateZonedDateTime().ifPresent(d => {
				result.pubdate = parseDateTime(d);
			});
			return result;
		});

	}
	
	/*
	Main function to get all the content for the home page and such
	*/
	public function getItems(perpage=10, start=1) {
		/*
		Note, I had bound params for limit and offset, but they stopped
		working on a new MySQL install. Google seemed to imply they *never* should
		have worked. So... yeah, not sure.
		*/
		return queryExecute('
		select i.title, i.posted, i.url, i.content, b.name, i.screenshot
		from items i
		inner join blogs b 
		on i.blogidfk = b.id
		order by i.posted desc
		limit #perpage#
		offset #arguments.start-1#
		', {
		}, { returnType:'array' });

	}

	public function getItemCount() {
		q = queryExecute('select count(id) as total from items');
		return q.total[1];
	}

	public function getItemsNeedingScreenshots() {
		q = queryExecute('select id, url from items where screenshot is null order by posted desc limit 5', {}, { returnType:'array'});
		return q;
	}

	/*
	I'm the main function to handle 
		a) getting blogs
		b) looping over each
		c) getting items for each
		d) checking if an item is a new item
		e) if not, inserted it
	*/
	public function loadFeeds() {
		blogs = getBlogs();
		blogs.each(b => {
			items = fetchItemsForFeed(b);
			println('For blog #b.name# (#b.id#) I got #items.len()# items.');
			items.each(i => {

				/*
				 I could do this inline in the code below, but I wanted to call
				 it out more. We are only using 1k chars of content. We may end up
				 displaying even less, and I may change this later.

				 We are also going to remove all tags.
				 */
				content = i.content.rereplace('<.*?>','','all').left(1000);

				queryExecute('
				insert ignore into items(blogidfk, title, posted, url, content)
				values(:blogidfk, :title, :posted, :url, :content)
				', {
					blogidfk: b.id, 
					title: i.title,
					posted: i.pubdate,
					url: i.link,
					content:content
				});
			});

		});
	}

	public function updateScreenshot(itemid, filename) {
		queryExecute('update items set screenshot = :filename where id = #itemid#', { filename: filename });

	}

}